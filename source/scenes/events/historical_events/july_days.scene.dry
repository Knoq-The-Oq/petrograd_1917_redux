title: [+ month : month +] Crisis
subtitle: The people demand "All Power to the Soviets" and an end to the war...
new-page: true 
view-if: month >= 7 and (june_offensive_defeat = 1 or military_progress < 0) and war_weariness >= 55 and july_days_seen = 0 and government_form != 1 and not in_socialist_coalition
on-arrival: july_days_seen += 1; workers_b += 12*(1-b_dissent); soldiers_b += 15*(1-b_dissent); b_threat += 1;
face-image: img/riot.jpg
tags: event 

= [+ month : month +] Crisis

The workers and soldiers of Petrograd are increasingly tired of the war; our recent defeats have not helped. Hundreds of thousands of workers, soldiers, and sailors have marched upon the Soviet, demanding that the Soviet take power over the Provisional Government.

- @take_power
- @demand_all_socialist
- @negotiate
- @nothing
- @support
- @chaos

@take_power
title: This is an opportunity to finally take power for the Soviet!
unavailable-subtitle: [? if (player_party = "b" and b_prov_gov_policy < 4) or (player_party = "m" and m_in_exco and m_prov_gov_policy < 4) or (player_party = "sr" and sr_in_exco and sr_prov_gov_policy < 4): Our party does not support the armed overthrow of the government. ?][? if not player_in_exco: We must be in the Soviet Executive Committee. ?]
view-if: (player_party = "b") or (player_party = "m" and m_in_exco) or (player_party = "sr" and sr_in_exco)
choose-if: (player_party = "b" and b_in_exco and b_prov_gov_policy >= 4) or (player_party = "m" and m_in_exco and m_prov_gov_policy >= 4) or (player_party = "sr" and sr_in_exco and sr_prov_gov_policy >= 4)

Many Bolsheviks would use this crisis as an opportunity to fight and overthrow the Provisional Government.

[? if rev_progress < 4: Our revolutionary preparations are inadequate. Can we really take power at this moment? ?]

- @info: What is the distribution of forces?
- @nothing: No, we are not ready...

@info

On our side are the troops of the garrison, [+ garrison_strength +] thousand in total. Support for the Bolsheviks is [+ garrison_b_loyalty : strength_2 +]. The workers' militia has [+ workers_militia_strength +] thousand members, and support for the Bolsheviks is [+ workers_militia_b_loyalty : strength_2 +]. 

[? if rev_progress < 4: Despite our popular support, we have not yet made tactical preparations for this action. Our leadership and organization are not yet in place. ?]

Opposing us will be the city militias, the loyal troops (Cossacks and Death Battalions), and the Mensheviks and SRs, most of whose members will oppose us (with the exception of the Left-SRs. [? if prov_gov_legitimacy < 40: The Provisional Government is weak and seen as illegitimate. This is good for us. ?]

- @take_power_2: Yes, we are ready to fight!
- @nothing: No, we are not ready...

@take_power_2
on-arrival: join_bolshevik = 1 if player_party = "sr" or player_party = "m"

Finally, our organization acts. The armed demonstrators are given direction, and turned against the Winter Palace...

- @the_battle: There will be a battle...

@the_battle
on-arrival: prov_gov_advantage = 1 if mrc = 0;
go-to: civil_war
set-jump: post_battle

@post_battle
go-to: prov_gov_win if prov_gov_victory; rev_win if revolutionary_victory

@prov_gov_win
on-arrival: workers_b -= 15; soldiers_b -= 15; workers_militia_b_loyalty -= 15;
achievement: rev_failed

= Provisional Government Victory

In the end, our forces were not strong enough to take on the Provisional Government. With many casualties, our troops scattered, and our leadership arrested. This is the end of the Bolsheviks, for now.

- @game_over

@rev_win

= Bolshevik Victory

We have won! Our forces have taken control of key government locations, and arrested their leaders. The bourgeois government will be consigned to the dustbin of history, and the dictatorship of the proletariat will commence.

- @game_over



# TODO: have some nonviolent paths to forming an all-socialist government that do not rely on Kadet relationships
@use_negotiations
title: Use the protests to convince the Kadets to resign from the Provisional Government, and the formation of an all-socialist government.
view-if: (player_party = "m" and m_in_exco) or (player_party = "sr" and sr_in_exco)

@negotiate
title: We can negotiate and wait out the demonstrators.
subtitle: The Soviet is not ready to take power right now.
view-if: (player_party = "b" and b_in_exco) or (player_party = "m" and m_in_exco) or (player_party = "sr" and sr_in_exco) or (player_party = "k")
on-arrival: kadets_resign = 1 if prime_minister = "Lvov" and player_party != "k"; prime_minister = "Kerensky" if kadets_resign; prime_minister_party = "SR" if kadets_resign; prime_minister_party = "Right-SR" if right_sr_split and kadets_resign;

After some streetfighting and arrests, the protestors eventually grow tired, and return to their homes and barracks. The Provisional Government is still in place.

[? if kadets_resign: Due to the protests, and the earlier response to the Ukrainian Central Rada, the government of Prime Minister Lvov has resigned. Alexander Kerensky uses this as an opportunity to gather more power - he is now Prime Minister. ?]

- @demand_all_socialist
- @let_socialists
- @shuffle_cabinet
- @root: Continue...

@let_socialists
title: Let the socialists enter the government to alleviate the crisis.
view-if: player_party = "k" and k_in_gov
on-arrival: kadets_resign = 1 if prime_minister = "Lvov"; prime_minister = "Kerensky" if prime_minister = "Lvov"; prime_minister_party = "SR" if prime_minister_party = "Independent"; prov_gov_legitimacy += 8; m_k_relation += 5*(1-k_dissent); sr_k_relation += 5*(1-k_dissent); soviet_cooperation += 0.1; agriculture_minister = "Chernov" if agriculture_minister_party = "Kadet"; agriculture_minister_party = "SR" if agriculture_minister_party = "Kadet"; interior_minister = "Tsereteli" if interior_minister = "Lvov"; interior_minister_party = "Menshevik" if interior_minister_party = "Independent"; labor_minister = "Skobelev" if labor_minister_party != "Menshevik" and labor_minister_party != "SR"; labor_minister_party = "Menshevik" if labor_minister_party != "Menshevik" and labor_minister_party != "SR"

[? if kadets_resign: Due to the protests, and the earlier response to the Ukrainian Central Rada, the government of Prime Minister Lvov has resigned. Alexander Kerensky uses this as an opportunity to gather more power - he is now Prime Minister. In addition, some Mensheviks and SRs have joined the Provisional Government as ministers. Hopefully this will restore the government's legitimacy and renew relations with the Soviet and the working class. ?]

@nothing
title: We are paralyzed by indecision!
view-if: player_party = "b" or (player_party = "sr" and not sr_in_gov) or (player_party = "m" and not m_in_gov)
on-arrival: kadets_resign = 1 if prime_minister = "Lvov"; prime_minister = "Kerensky" if prime_minister = "Lvov"; prime_minister_party = "SR" if prime_minister_party = "Independent"; prime_minister_party = "Right-SR" if right_sr_split;

After some streetfighting and arrests, the protestors eventually grow tired, and return to their homes and barracks.

[? if kadets_resign: Due to the protests, and the earlier response to the Ukrainian Central Rada, the government of Prime Minister Lvov has resigned. Alexander Kerensky uses this as an opportunity to gather more power - he is now Prime Minister. ?]

- @shuffle_cabinet
- @root: Continue...

@support
title: Support the protestors!
view-if: player_party = "b" or (player_party = "sr" and not sr_in_gov) or (player_party = "m" and not m_in_gov)
on-arrival: kadets_resign = 1 if prime_minister = "Lvov"; prime_minister = "Kerensky" if prime_minister = "Lvov"; prime_minister_party = "SR" if prime_minister_party = "Independent"; prime_minister_party = "Right-SR" if right_sr_split; m_k_relation -= 8; sr_k_relation -= 8;

After some streetfighting and arrests, the protestors eventually grow tired, and return to their homes and barracks.

[? if kadets_resign: Due to the protests, and the earlier response to the Ukrainian Central Rada, the government of Prime Minister Lvov has resigned. Alexander Kerensky uses this as an opportunity to gather more power - he is now Prime Minister. ?]

- @demand_all_socialist
- @root: Continue...

@chaos
title: Utter chaos...
view-if: player_party = "k" and not k_in_gov
on-arrival: kadets_resign = 1 if prime_minister = "Lvov"; prime_minister = "Kerensky" if prime_minister = "Lvov"; prime_minister_party = "SR" if prime_minister_party = "Independent"; prov_gov_legitimacy -= 10; workers_militia_strength -= 5;

After some streetfighting and arrests, the protestors eventually grow tired, and return to their homes and barracks. The legitimacy and stability of the government is shaken, however, and the spectre of an uprising from the left remains.


@shuffle_cabinet
title: We can use this opportunity to increase our participation in the Provisional Government...
view-if: prime_minister = "Kerensky" and (player_party = "sr" or (player_party = "m" and m_sr_relation >= 50))
on-arrival: m_influence += 15; sr_influence += 15; m_in_gov = 1; sr_in_gov = 1;
go-to: sr_join_prov_gov if player_party = "sr"; m_join_prov_gov if player_party = "m"

Due to the crisis, the liberals in the Provisional Government are inclined to allow the socialist parties greater participation the government. They hope that socialists can stabilize the relation between the government and the streets.

@demand_all_socialist
title: Demand an all-socialist Provisional Government!
subtitle: It is time for the Kadets to leave the Provisional Government, and form a government of only the socialist parties.
unavailable-subtitle: [? if (player_party = "m" and m_prov_gov_policy < 3) or (player_party = "sr" and sr_prov_gov_policy < 3): The party does not support the formation of an all-socialist government. ?][? if (player_party = "m" and m_k_relation < 40) or (player_party = "sr" and sr_k_relation < 40) : Our relations with the Kadets are not good enough. ?][? if (player_party = "m" and m_sr_relation < 55) : Our relations with the SRs are not good enough. ?][? if (player_party = "sr" and m_sr_relation < 55) : Our relations with the Mensheviks are not good enough. ?][? if not player_in_exco: We must be in the Soviet Executive Committee. ?]
view-if:((player_party = "m" and not m_defensist_split) or (player_party = "sr" and not right_sr_split)) and not in_socialist_coalition
choose-if: (player_party = "m" and m_prov_gov_policy >= 3 and m_sr_relation >= 55 and m_k_relation >= 40 and m_in_exco) or (player_party = "sr" and sr_prov_gov_policy >= 3 and m_sr_relation >= 55 and sr_k_relation >= 40 and sr_in_exco)
on-arrival: k_in_gov = 0; sr_in_gov = 1; m_in_gov = 1; in_socialist_coalition = 1; prime_minister = "Kerensky"; prime_minister_party = "SR"; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; justice_minister = "Kerensky"; justice_minister_party = "SR"; interior_minister = "Tsereteli"; interior_minister_party = "Menshevik"; finance_minister = "Voitinsky"; finance_minister_party = "Menshevik"; war_minister = "Verkhovsky"; war_minister_party = "SR"; foreign_minister = "Tereshchenko"; foreign_minister_party = "Independent"; transport_minister = "Plekhanov"; transport_minister_party = "Menshevik"; trade_minister = "Groman"; trade_minister_party = "Menshevik"; food_minister = "Kondratiev"; food_minister_party = "SR"; m_influence = m_sr_relation - 35; sr_influence = m_sr_relation - 45; m_k_relation -= 10; sr_k_relation -= 10; prov_gov_legitimacy += 20; workers_sr += 5*(1-sr_dissent); soldiers_sr += 8*(1-sr_dissent); workers_m += 8*(1-m_dissent); soldiers_m += 6*(1-m_dissent);

We have successfully leveraged our relations with the Kadets and the [? if player_party = "sr": Mensheviks ?][? if player_party = "m": SRs ?] into forming a new government consisting solely of the socialist parties, with the Kadets being removed from power!

There is celebration among the workers and soldiers that their representatives are entering power. However, we must make sure to not squander our opportunity, or else the forces of reaction and extremism may take advantage.

What should be the form of the all-socialist government?

- @sov_gov: The government should be based in the Soviets!
- @parliament: We should maintain the parliamentary structure of the Provisional Government.

@sov_gov
on-arrival: government_form = 1; prov_gov_legitimacy += 10; m_k_relation -= 10; sr_k_relation -= 10; m_b_relation += 5; b_sr_relation += 5; workers_m += 8*(1-m_dissent); workers_sr += 8*(1-sr_dissent);
go-to: soviet_government_formation

@parliament

We will maintain the parliamentary and bourgeois trappings of the Provisional Government. Without the full development of capitalism, there can be no dictatorship of the proletariat.

- @demand_tsereteli
- @m_join_prov_gov: We must choose our ministries.
- @sr_join_prov_gov: We must choose our ministries.

@demand_tsereteli
title: Demand Tsereteli be Prime Minister instead of Kerensky.
unavailable-subtitle: [? if m_sr_relation < 55 : Our relations with the SRs are not good enough. ?][? if m_k_relation < 30 : Our relations with the Kadets are not good enough. ?]
view-if: player_party = "m"
choose-if: m_sr_relation >= 55 and m_k_relation >= 30
on-arrival: prime_minister = "Tsereteli"; prime_minister_party = "Menshevik"; m_influence -= 10; m_sr_relation -= 5;

We have successfully convinced our SR partners that Tsereteli should be the prime minister instead of Kerensky. This slightly lowers our relationship with the SRs.

- @m_join_prov_gov: We must choose our ministries.
